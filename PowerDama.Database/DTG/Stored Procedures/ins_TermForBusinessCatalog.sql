
/* KUWAIT TURKISH PARTICIPATION BANK INC.
*
*  All rights are reserved. Reproduction or transmission in whole or in part, in
*  any form or by any means, electronic, mechanical or otherwise, is prohibited
*  without the prior written consent of the copyright owner.
*
*
*  Generator Information
*		Generator				: Manual
*		Generated By			: karadayi
*		Generated Date			: 31/05/2016
*		File Version			: 1.0
*		Purpose					: <Lütfen açıklama giriniz.>
*		Last Modified By		: karadayi
*		Last Modification Date	: 12/06/2017
*
*/
CREATE PROCEDURE [DTG].[ins_TermForBusinessCatalog]
(
	@TermId INT = NULL,
	@Name VARCHAR(200),
	@NameEN VARCHAR(200),
	@Description VARCHAR(2000),
	@DescriptionEN VARCHAR(2000) = NULL,
	@ModuleId INT,
	@Type TINYINT,
	@DataType VARCHAR(5) = NULL,
	@Sensitivity TINYINT = NULL,
	@DataOwner INT,
	@HashTag VARCHAR(50) = NULL
)
AS
	SET NOCOUNT ON
	BEGIN

	DECLARE
	@ExecuteString NVARCHAR(500),
	@TermCode VARCHAR(30),
	@InsertedId INT

	-- CONSTANT VALUES
	DECLARE
	@C_BUSINESS_TERM TINYINT = 1,
	@C_DATA_TYPE TINYINT = 3,
	@C_ENTITY TINYINT = 0,
	@C_ATTRIBUTE TINYINT = 1

	IF @TermId IS NULL
	BEGIN
		SET @ExecuteString = N'EXEC [DTG].[sel_TermCodeByModuleId] ''' + CAST(@ModuleId AS VARCHAR) + ''''

		CREATE TABLE #TMP (TermCode VARCHAR(30) NOT NULL)
	
		INSERT INTO #TMP
		EXECUTE sp_executesql @ExecuteString

		SELECT @TermCode = TermCode FROM #TMP
	
		INSERT INTO [DTG].[Term]
		(
			TermCode,
			Name,
			NameEN,
			[Description],
			DescriptionEN,
			Level1Domain,
			[Type],
			Sensitivity,
			DataOwner,
			TermType,
			HashTag
		)
		VALUES
		(
			@TermCode,
			@Name,
			@NameEN,
			@Description,
			@DescriptionEN,
			@ModuleId,
			@Type,
			@Sensitivity,
			@DataOwner,
			@C_BUSINESS_TERM,
			@HashTag
		)

		SELECT @InsertedId = CAST(SCOPE_IDENTITY() AS INT)

		IF @Type = @C_ATTRIBUTE AND @DataType IS NOT NULL
		BEGIN
			EXEC [DTG].[ins_TermRule] @InsertedId, @C_DATA_TYPE, @DataType
		END

		UPDATE T
		   SET T.TermCode = CAST(T.TermId AS VARCHAR)
		  FROM [DTG].[Term] AS T
	     WHERE T.TermId = @InsertedId

		SELECT @InsertedId
	END
	ELSE
	BEGIN
		UPDATE [DTG].[Term]
		   SET Name = @Name,
		       NameEN = @NameEN,
			   [Description] = @Description,
			   DescriptionEN = @DescriptionEN,
			   Level1Domain = @ModuleId,
			   [Type] = @Type,
			   Sensitivity = @Sensitivity,
			   DataOwner = @DataOwner,
			   TermType = @C_BUSINESS_TERM,
			   HashTag = ISNULL(@HashTag, HashTag)
		 WHERE TermId = @TermId

		IF @Type = @C_ATTRIBUTE AND @DataType IS NOT NULL
		BEGIN
		    IF EXISTS(SELECT TOP 1 1 FROM [DTG].[TermRule] WITH (NOLOCK) WHERE TermId = @TermId AND TemplateType = @C_DATA_TYPE)
			BEGIN
				UPDATE [DTG].[TermRule]
				   SET Value = @DataType
				 WHERE TermId = @TermId
				   AND TemplateType = @C_DATA_TYPE
			END
			ELSE
			BEGIN
				EXEC [DTG].[ins_TermRule] @TermId, @C_DATA_TYPE, @DataType
			END
		END
		ELSE IF @Type = @C_ENTITY
		BEGIN
			DELETE FROM [DTG].[TermRule]
			 WHERE TermId = @TermId
			   AND TemplateType = @C_DATA_TYPE
		END
	END
END